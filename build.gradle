buildscript {
    ext {
        kotlin_version = '1.3.11'
        junit_version = '4.12'
        slf4j_version = '1.7.28'

        spring_version = '5.1.5.RELEASE'
        spring_boot_version = '2.1.3.RELEASE'
        spring_cloud_version = '2.1.0.RELEASE'
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
}

allprojects {

    group 'com.labijie'
    version '1.0.0'

    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.8

    configurations {
        all*.exclude group: "org.apache.logging.log4j"
    }


    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }


    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    }
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = "javadoc"
        from javadoc
    }

    artifacts {
        //archives jar
        archives sourcesJar
        archives javadocJar
    }

    signing {
        if (project.hasProperty('signing.keyId') &&
                project.hasProperty('signing.secretKeyRingFile') &&
                project.hasProperty("signing.password")) {
            sign configurations.archives
        } else {
            println "Signing information missing/incomplete for ${project.name}"
        }
    }
    //sign configurations.archives

    uploadArchives {
        if (!project.name.startsWith("dummy")) {

            System.out.println("deployer --->" + project.name)

            def art = "spring-cloud-stream-binder-memory" + (project.name == "core" ? "" : "-" + project.name)
            repositories {
                mavenDeployer {
                    repository(url: "file://" + System.properties['user.home'] + "/.m2/repository")
                    pom.project {
                        artifactId art
                        version project.rootProject.version
                    }
                }

                //gradle -Dmu="admin" -Dmp="password" uploadArchives
                def u = project.property("oss-releases.username") != null
                        ? project.property("oss-releases.username") : System.properties.getProperty("mu")
                def p = project.property("oss-releases.password") != null
                        ? project.property("oss-releases.password") : System.properties.getProperty("mp")


                if (u != null && p != null && !u.isEmpty() && !p.isEmpty()) {

                    System.out.println("maven deploy user: " + u)


                    mavenDeployer {
                        if (project.hasProperty('signing.keyId') &&
                                project.hasProperty('signing.secretKeyRingFile') &&
                                project.hasProperty("signing.password")) {
                            beforeDeployment { MavenDeployment deployment ->
                                System.out.println("sign: " + u)
                                signing.signPom(deployment)
                            }
                        }

                        pom.project {
                            name art
                            description 'Memory queue implement for spring cloud stream application'
                            artifactId art
                            version project.rootProject.version
                            url 'https://github.com/endink/spring-cloud-stream-binder-memory'
                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'labijie.com'
                                }
                            }
                            scm {
                                url 'scm:git@github.com:endink/spring-cloud-stream-binder-memory.git'
                                connection 'scm:git@github.com:endink/spring-cloud-stream-binder-memory.git'
                                developerConnection 'git@github.com:endink/spring-cloud-stream-binder-memory.git'
                            }

                            developers {
                                developer {
                                    id 'labijie.com'
                                    name 'AndersXiao'
                                    email 'sharping@outlook.com'
                                }
                            }
                        }

                        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                            authentication(userName: u, password: p)
                        }

                        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                            authentication(userName: u, password: p)
                        }
                    }
                }
            }
        }
    }
}